bring Vortex
bring File
bring Err


var homePage: str, homePageLoadErr: Err::Result = File::read("homePage.html", "r")  /; Handle GET request for the home page

if (homePageLoadErr != Nir) {
  print(homePageLoadErr.getMessage(), "\n")
}

fun handleHomePage(req: Vortex::HttpRequest, res: Vortex::HttpResponse) -> nthg {
  var err: Err::Result = res.send({
    status: 200,
    content_type: "text/html",
    body: homePage
  })
  if err != Nir {
    print("Error sending response: ", err.getMessage(), "\n")
  }
}


fun handleGet(req: Vortex::HttpRequest, res: Vortex::HttpResponse) -> nthg {
  var err: Err::Result = res.send({
    status: 200,
    content_type: "application/json",
    body: `{
      "message": "Welcome to the Flow-Wing Programming Language!"
    }`
  })
  if err != Nir {
    print("Error sending response: ", err.getMessage(), "\n")
  }
}

const port: int = 8080
print("Flow-Wing server running on port: ", port, "\n")
var server: Vortex::Server = new Vortex::Server(port)
server.setRoute("GET", "/", handleHomePage)
server.setRoute("GET", "/api/welcome", handleGet)
server.start()
